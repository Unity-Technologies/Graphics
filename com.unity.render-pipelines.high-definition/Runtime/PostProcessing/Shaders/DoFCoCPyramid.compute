#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Common.hlsl"
#include "Packages/com.unity.render-pipelines.high-definition/Runtime/ShaderLibrary/ShaderVariables.hlsl"

#pragma only_renderers d3d11 playstation xboxone vulkan metal switch

#pragma kernel KMainCoCPyramid

CBUFFER_START(cb0)
float4 _Params;
CBUFFER_END

#define NumMips            _Params.x

RW_TEXTURE2D_X(float, _InputTexture);

RW_TEXTURE2D_X(float, _OutputMip1);
RW_TEXTURE2D_X(float, _OutputMip2);
RW_TEXTURE2D_X(float, _OutputMip3);
RW_TEXTURE2D_X(float, _OutputMip4);
RW_TEXTURE2D_X(float, _OutputMip5);
RW_TEXTURE2D_X(float, _OutputMip6);

#define GROUP_SIZE 32

groupshared float gs_cache[GROUP_SIZE * GROUP_SIZE];

void StorePixel(uint index, float color)
{
    gs_cache[index] = color;
}

float LoadPixel(uint index)
{
    return gs_cache[index];
}

float MaxOp(float a, float b)
{
    return abs(a) > abs(b) ? a : b;
}

// Generate six mips in one pass
[numthreads(GROUP_SIZE, GROUP_SIZE, 1)]
void KMainCoCPyramid(uint3 dispatchThreadId : SV_DispatchThreadID, uint groupIndex : SV_GroupIndex)
{
    UNITY_XR_ASSIGN_VIEW_INDEX(dispatchThreadId.z);

    uint2 ul = dispatchThreadId.xy << 1u;

    // First mip
    float color = _InputTexture[COORD_TEXTURE2D_X(ul)];
    color = MaxOp(color, _InputTexture[COORD_TEXTURE2D_X(ul + uint2(1u, 0u))]);
    color = MaxOp(color, _InputTexture[COORD_TEXTURE2D_X(ul + uint2(0u, 1u))]);
    color = MaxOp(color, _InputTexture[COORD_TEXTURE2D_X(ul + uint2(1u, 1u))]);

    // TODO: also compute and pack the min value
    StorePixel(groupIndex, color);

    _OutputMip1[COORD_TEXTURE2D_X(dispatchThreadId.xy)] = color;

    if (NumMips <= 1.0)
        return;

    GroupMemoryBarrierWithGroupSync();

    // Second mip - checks that X and Y are even
    if ((groupIndex & 0x21) == 0)
    {
        color = MaxOp(color, LoadPixel(groupIndex + 1u));
        color = MaxOp(color, LoadPixel(groupIndex + GROUP_SIZE));
        color = MaxOp(color, LoadPixel(groupIndex + GROUP_SIZE + 1u));

        StorePixel(groupIndex, color);

        _OutputMip2[COORD_TEXTURE2D_X(dispatchThreadId.xy / 2u)] = color;
    }

    if (NumMips <= 2.0)
        return;

    GroupMemoryBarrierWithGroupSync();

    // Third mip - checks that X and Y are multiples of four
    if ((groupIndex & 0x63) == 0)
    {
        color = MaxOp(color, LoadPixel(groupIndex + 2u));
        color = MaxOp(color, LoadPixel(groupIndex + 2u * GROUP_SIZE));
        color = MaxOp(color, LoadPixel(groupIndex + 2u * GROUP_SIZE + 2u));
        StorePixel(groupIndex, color);

        _OutputMip3[COORD_TEXTURE2D_X(dispatchThreadId.xy / 4u)] = color;
    }

    if (NumMips <= 3.0)
        return;

    GroupMemoryBarrierWithGroupSync();
    
    // Fourth mip - checks that X and Y are multiples of 8
    if ((groupIndex & 0xE7) == 0)
    {
        color = MaxOp(color, LoadPixel(groupIndex + 4u));
        color = MaxOp(color, LoadPixel(groupIndex + 4u * GROUP_SIZE));
        color = MaxOp(color, LoadPixel(groupIndex + 4u * GROUP_SIZE + 4u));
        StorePixel(groupIndex, color);

        _OutputMip4[COORD_TEXTURE2D_X(dispatchThreadId.xy / 8u)] = color;
    }

    if (NumMips <= 4.0)
        return;

    GroupMemoryBarrierWithGroupSync();

    // Fifth mip - checks that X and Y are multiples of 16
    if ((groupIndex & 0x1EF) == 0)
    {
        color = MaxOp(color, LoadPixel(groupIndex + 8u));
        color = MaxOp(color, LoadPixel(groupIndex + 8u * GROUP_SIZE));
        color = MaxOp(color, LoadPixel(groupIndex + 8u * GROUP_SIZE + 8u));

        _OutputMip5[COORD_TEXTURE2D_X(dispatchThreadId.xy / 16u)] = color;
    }

    if (NumMips <= 5.0)
        return;

    // Last mip - only one thread
    if (groupIndex == 0)
    {
        color = MaxOp(color, LoadPixel(groupIndex + 16u));
        color = MaxOp(color, LoadPixel(groupIndex + 16u * GROUP_SIZE));
        color = MaxOp(color, LoadPixel(groupIndex + 16u * GROUP_SIZE + 16u));

        _OutputMip6[COORD_TEXTURE2D_X(dispatchThreadId.xy / 32u)] = color;
    }
}
