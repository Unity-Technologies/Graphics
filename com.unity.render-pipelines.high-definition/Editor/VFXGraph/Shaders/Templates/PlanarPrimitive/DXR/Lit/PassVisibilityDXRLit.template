
        Pass
        {
            Name "VisibilityDXR"
            Tags{ "LightMode" = "VisibilityDXR" }

            HLSLPROGRAM
            #pragma target 4.5
            #pragma only_renderers d3d11 xboxseries ps5
            #pragma raytracing surface_shader
            #pragma multi_compile _ TRANSPARENT_COLOR_SHADOW
            #pragma skip_variants INSTANCING_ON

	        #if !USE_ALPHA_TEST && IS_TRANSPARENT_PARTICLE
	        #define USE_ALPHA_TEST 1
	        #endif

            // Global includes
            #include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Common.hlsl"
            #include "Packages/com.unity.render-pipelines.high-definition/Runtime/ShaderLibrary/ShaderVariables.hlsl"
            #include "Packages/com.unity.render-pipelines.high-definition/Runtime/RenderPipeline/ShaderPass/ShaderPass.cs.hlsl"

            // Define the shader pass
            #define SHADERPASS SHADERPASS_RAYTRACING_VISIBILITY

            // Need to override the macros for ray tracing
            #include "Packages/com.unity.render-pipelines.high-definition/Runtime/RenderPipeline/Raytracing/Shaders/RaytracingMacros.hlsl"

            // Shader variables
            #include "Packages/com.unity.render-pipelines.high-definition/Runtime/RenderPipeline/Raytracing/Shaders/ShaderVariablesRaytracing.hlsl"

            // VFX includes
	        ${VFXIncludeRP("VFXLitVaryings.template")}
            ${VFXIncludeRP("Templates/PlanarPrimitive/DXR/Lit/RayTracingInputStructure.template")}
            ${VFXPerPassInclude}
            ${VFXIncludeRP("VFXLit.template")}

            // Needs to be included after the VFX includes
            ${VFXIncludeRP("Templates/PlanarPrimitive/DXR/Lit/IntersectionFunctions.template")}

            // Material includes
            #include "Packages/com.unity.render-pipelines.high-definition/Runtime/Material/Material.hlsl"
            #include "Packages/com.unity.render-pipelines.high-definition/Runtime/RenderPipeline/Raytracing/Shaders/RaytracingIntersection.hlsl"
            #include "Packages/com.unity.render-pipelines.high-definition/Runtime/RenderPipeline/Raytracing/Shaders/Common/RayTracingProcedural.hlsl"
            #include "Packages/com.unity.render-pipelines.high-definition/Runtime/RenderPipeline/Raytracing/Shaders/RayTracingCommon.hlsl"
            #include "Packages/com.unity.render-pipelines.high-definition/Runtime/Material/NormalBuffer.hlsl"

            ${VFXGeneratedBlockFunction}

            [shader("closesthit")]
            void ClosestHit(inout RayIntersectionVisibility rayIntersection : SV_RayPayload, AttributeData attributeData : SV_IntersectionAttributes)
            {
                UNITY_XR_ASSIGN_VIEW_INDEX(DispatchRaysIndex().z);

                // Compute the distance of the ray
                rayIntersection.t = RayTCurrent();

                // Index needs to be available in the context for the attribute load to work
                ${VFXGetIndexFromRTPrimitiveIndex}
                ${VFXLoadGraphValues}
                // Grab the current particle's AABB
                AABB particleAABB = FetchPrimitiveAABB(PrimitiveIndex(), VFX_AABB_COUNT, instanceIndex);
                float3 inputVertexPosition = 0.0;
                float3 currentOS = (particleAABB.maxPosOS + particleAABB.minPosOS) * 0.5;
                float3 currentWS = TransformObjectToWorld(currentOS);

                ${VFXVertexComputeDisplacement}
                rayIntersection.velocity = displacement;
            }

            // Include to evaluate the material data
            ${VFXIncludeRP("Templates/PlanarPrimitive/DXR/Lit/EvaluateMaterialData.template")}

            [shader("anyhit")]
            void AnyHitVisibility(inout RayIntersectionVisibility rayIntersection : SV_RayPayload, AttributeData attributeData : SV_IntersectionAttributes)
            {
                UNITY_XR_ASSIGN_VIEW_INDEX(DispatchRaysIndex().z);

                // Compute the distance of the ray
                rayIntersection.t = RayTCurrent();

                // Evaluate the view direction
                float3 viewWS = -WorldRayDirection();

                // Evaluate the position
                float3 positionRWS = WorldRayOrigin() + rayIntersection.t * WorldRayDirection();

                // Evaluate the position input
                PositionInputs posInput = GetPositionInput(0, _ScreenSize.zw, positionRWS);

                // Output structures
                SurfaceData surfaceData = (SurfaceData)0;
                BuiltinData builtinData = (BuiltinData)0;
                BSDFData bsdfData = (BSDFData)0;
                PreLightData preLightData = (PreLightData)0;
                float velocity = 0.0f;

                // Evaluate all the required material data
                EvaluateLitMaterialData(posInput, attributeData, surfaceData, builtinData, bsdfData, preLightData, velocity);

            #if defined(TRANSPARENT_COLOR_SHADOW) && defined(_SURFACE_TYPE_TRANSPARENT)
                // Override the veloctity for this value
                rayIntersection.velocity = velocity;

                // Adjust the color based on the transmittance or opacity
                #if HAS_REFRACTION
                    rayIntersection.color *= lerp(surfaceData.transmittanceColor, float3(0.0, 0.0, 0.0), 1.0 - surfaceData.transmittanceMask);
                #else
                    rayIntersection.color *= (1.0 - builtinData.opacity);
                #endif

                // Ignore to move to the following intersections
                IgnoreHit();
            #else
                // If this fella is opaque, then we need to stop
                rayIntersection.color = float3(0.0, 0.0, 0.0);
                AcceptHitAndEndSearch();
            #endif
            }

            // Inject the intersection shader
            ${VFXIncludeRP("Templates/PlanarPrimitive/DXR/Common/IntersectionShader.template")}
            ENDHLSL
        }
