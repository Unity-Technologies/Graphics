// =============== Convolves transmitted radiance with the Disney diffusion profile ================

//--------------------------------------------------------------------------------------------------
// Definitions
//--------------------------------------------------------------------------------------------------

// #pragma enable_d3d11_debug_symbols
#pragma only_renderers d3d11 playstation xboxone xboxseries vulkan metal switch

#pragma kernel SubsurfaceScattering
#pragma kernel PackDiffusionProfile PACK_DIFFUSION_PROFILE

#pragma multi_compile _ ENABLE_MSAA
#pragma multi_compile _ USE_DOWNSAMPLE
#pragma multi_compile _ USE_SSS_OCCLUSION

// TODO: use sharp load hoisting on PS4.

// Tweak parameters.
#define SSS_BILATERAL_FILTER  1
#define SSS_USE_LDS_CACHE     1 // Use LDS as an L0 texture cache.
#define SSS_RANDOM_ROTATION   1 // Hides undersampling artifacts with high-frequency noise. TAA blurs the noise.
#define SSS_USE_TANGENT_PLANE 0 // Improves the accuracy of the approximation(0 -> 1st order). High cost. Does not work with back-facing normals.
#define SSS_CLAMP_ARTIFACT    0 // Reduces bleeding. Use with SSS_USE_TANGENT_PLANE.
#define SSS_DEBUG_LOD         0 // Displays the sampling rate: green = no filtering, blue = 1 sample, red = _SssSampleBudget samples.
#define SSS_DEBUG_NORMAL_VS   0 // Allows detection of back-facing normals.
#define SSS_DEBUG_PATTERN     0 // Shows the sample pattern of one pixel

// Do not modify these.
#include "Packages/com.unity.render-pipelines.high-definition/Runtime/RenderPipeline/ShaderPass/ShaderPass.cs.hlsl"
#define SHADERPASS            SHADERPASS_SUBSURFACE_SCATTERING
#define GROUP_SIZE_1D         16
#define GROUP_SIZE_2D         (GROUP_SIZE_1D * GROUP_SIZE_1D)
#define TEXTURE_CACHE_BORDER  2
#define TEXTURE_CACHE_SIZE_1D (GROUP_SIZE_1D + 2 * TEXTURE_CACHE_BORDER)
#define TEXTURE_CACHE_SIZE_2D (TEXTURE_CACHE_SIZE_1D * TEXTURE_CACHE_SIZE_1D)

// Check for support of typed UAV loads from FORMAT_R16G16B16A16_FLOAT.
// TODO: query the format support more precisely.
#if !(defined(SHADER_API_PSSL) || defined(SHADER_API_XBOXONE) || defined(SHADER_API_GAMECORE)) || defined(ENABLE_MSAA)
#define USE_INTERMEDIATE_BUFFER
#endif

//--------------------------------------------------------------------------------------------------
// Included headers
//--------------------------------------------------------------------------------------------------

#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Common.hlsl"
#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Packing.hlsl"
#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Sampling/Sampling.hlsl"
#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/SpaceFillingCurves.hlsl"
#include "Packages/com.unity.render-pipelines.high-definition/Runtime/ShaderLibrary/ShaderVariables.hlsl"
#include "Packages/com.unity.render-pipelines.high-definition/Runtime/Lighting/LightDefinition.cs.hlsl"
#include "Packages/com.unity.render-pipelines.high-definition/Runtime/Material/SubsurfaceScattering/SubsurfaceScattering.hlsl"
#include "Packages/com.unity.render-pipelines.high-definition/Runtime/RenderPipeline/HDStencilUsage.cs.hlsl"

//--------------------------------------------------------------------------------------------------
// Inputs & outputs
//--------------------------------------------------------------------------------------------------

int _SssSampleBudget;
int _SssDownsampleSteps;

TEXTURE2D_X(_DepthTexture);                           // Z-buffer
TEXTURE2D_X(_IrradianceSource);                       // Includes transmitted light
TEXTURE2D_X(_IrradianceSourceDownsampled);

StructuredBuffer<uint>  _CoarseStencilBuffer;

#ifdef USE_INTERMEDIATE_BUFFER
    RW_TEXTURE2D_X(float4, _CameraFilteringTexture);  // Target texture
#else
    RW_TEXTURE2D_X(float4, _CameraColorTexture);      // Target texture
#endif

#ifdef PACK_DIFFUSION_PROFILE
    RW_TEXTURE2D_X(uint, _DiffusionProfileIndexTexture);
#else
    TYPED_TEXTURE2D_X(uint, _DiffusionProfileIndexTexture);
#endif

//--------------------------------------------------------------------------------------------------
// Implementation
//--------------------------------------------------------------------------------------------------

// 6656 bytes used. It appears that the reserved LDS space must be a multiple of 512 bytes.
#if SSS_USE_LDS_CACHE
groupshared float2 textureCache0[TEXTURE_CACHE_SIZE_2D]; // {irradiance.rg}
groupshared float2 textureCache1[TEXTURE_CACHE_SIZE_2D]; // {irradiance.b, deviceDepth}
#endif
groupshared bool   processGroup;

#if SSS_USE_LDS_CACHE
void StoreSampleToCacheMemory(float4 value, int2 cacheCoord)
{
    int linearCoord = Mad24(TEXTURE_CACHE_SIZE_1D, cacheCoord.y, cacheCoord.x);

    textureCache0[linearCoord] = value.rg;
    textureCache1[linearCoord] = value.ba;
}

float4 LoadSampleFromCacheMemory(int2 cacheCoord)
{
    int linearCoord = Mad24(TEXTURE_CACHE_SIZE_1D, cacheCoord.y, cacheCoord.x);

    return float4(textureCache0[linearCoord],
                  textureCache1[linearCoord]);
}
#endif

float4 LoadSampleFromVideoMemory(int2 pixelCoord)
{
#ifndef USE_DOWNSAMPLE
    float3 irradiance = LOAD_TEXTURE2D_X(_IrradianceSource, pixelCoord).rgb;
#else
    float3 irradiance = LOAD_TEXTURE2D_X(_IrradianceSourceDownsampled, pixelCoord >> _SssDownsampleSteps).rgb;
#endif
    float  depth      = LOAD_TEXTURE2D_X(_DepthTexture,     pixelCoord).r;

    return float4(irradiance, depth);
}

// Returns {irradiance, linearDepth}.
float4 LoadSample(int2 pixelCoord, int2 cacheOffset)
{
    float4 value;

#if SSS_USE_LDS_CACHE
    int2 cacheCoord = pixelCoord - cacheOffset;
    bool isInCache  = max((uint)cacheCoord.x, (uint)cacheCoord.y) < TEXTURE_CACHE_SIZE_1D;

    if (isInCache)
    {
        value = LoadSampleFromCacheMemory(cacheCoord);
    }
    else
#endif
    {
        // Always load both irradiance and depth.
        // Avoid dependent texture reads at the cost of extra bandwidth.
        value = LoadSampleFromVideoMemory(pixelCoord);
    }

    value.a = LinearEyeDepth(value.a, _ZBufferParams);

    return value;
}

// Computes f(r, s)/p(r, s), s.t. r = sqrt(xy^2 + z^2).
// Rescaling of the PDF is handled by 'totalWeight'.
float3 ComputeBilateralWeight(float xy2, float z, float mmPerUnit, float3 S, float rcpPdf)
{
#if (SSS_BILATERAL_FILTER == 0)
    z = 0;
#endif

    // Note: we perform all computation in millimeters.
    // So we must convert from world units (using 'mmPerUnit') to millimeters.
#if SSS_USE_TANGENT_PLANE
    // Both 'xy2' and 'z' require conversion to millimeters.
    float r = sqrt(xy2 + z * z) * mmPerUnit;
    float p = sqrt(xy2) * mmPerUnit;
#else
    // Only 'z' requires conversion to millimeters.
    float r = sqrt(xy2 + (z * mmPerUnit) * (z * mmPerUnit));
    float p = sqrt(xy2);
#endif

    float area = rcpPdf;

#if 0
    // Boost the area associated with the sample by the ratio between the sample-center distance
    // and its orthogonal projection onto the integration plane (disk).
    area *= r / p;
#endif

#if SSS_CLAMP_ARTIFACT
    return saturate(EvalBurleyDiffusionProfile(r, S) * area);
#else
    return EvalBurleyDiffusionProfile(r, S) * area;
#endif
}


// Structure for debug information regarding the sample pattern
// Note: removed for non debug builds to not affect performance
struct DebugInfo
{
#if SSS_DEBUG_PATTERN
    // Input
    uint2 testCoord;
    // Output
    bool testCoordHit;
    bool withinRadius;
#endif
};

bool SameProfiles(uint sampleProfileIndex, uint currentProfileIndex)
{
#ifdef USE_SSS_OCCLUSION
    return sampleProfileIndex == currentProfileIndex;
#endif
    return true;
}

int GetDiffusionProfileIndexAtPosition(int2 position)
{
#if PACK_DIFFUSION_PROFILE
    // Load from RW texture has only 3 parameter (no mips) and we don't have macro for this one
    uint p = _DiffusionProfileIndexTexture[COORD_TEXTURE2D_X(uint2(position.x >> 1, position.y))];
#else
    uint p = LOAD_TEXTURE2D_X_LOD(_DiffusionProfileIndexTexture, uint2(position.x >> 1, position.y), 0);
#endif

    int sampleProfileIndex;

    if ((position.x & 1) == 0)
        sampleProfileIndex = p & 0xF;
    else
        sampleProfileIndex = p >> 4;

    return sampleProfileIndex;
}

void EvaluateSample(uint i, uint n, int2 pixelCoord, int2 cacheOffset,
                    float3 S, float d, float3 centerPosVS, float mmPerUnit, float pixelsPerMm,
                    float phase, float3 tangentX, float3 tangentY, float4x4 projMatrix, float linearDepth,
                    int currentProfileIndex, float3 borderAttenuationColor, inout DebugInfo debugInfo,
                    inout float3 totalIrradiance, inout float3 totalWeight)
{
    // The sample count is loop-invariant.
    const float scale  = rcp(n);
    const float offset = rcp(n) * 0.5;

    // The phase angle is loop-invariant.
    float sinPhase, cosPhase;
    sincos(phase, sinPhase, cosPhase);

    float r, rcpPdf;
    SampleBurleyDiffusionProfile(i * scale + offset, d, r, rcpPdf);

    float phi = SampleDiskGolden(i, n).y;
    float sinPhi, cosPhi;
    sincos(phi, sinPhi, cosPhi);

    float sinPsi = cosPhase * sinPhi + sinPhase * cosPhi; // sin(phase + phi)
    float cosPsi = cosPhase * cosPhi - sinPhase * sinPhi; // cos(phase + phi)

    float2 vec = r * float2(cosPsi, sinPsi);

    // Compute the screen-space position and the squared distance (in mm) in the image plane.
    int2 position; float xy2;

    #if SSS_USE_TANGENT_PLANE
        float3 relPosVS   = vec.x * tangentX + vec.y * tangentY;
        float3 positionVS = centerPosVS + relPosVS;
        float2 positionNDC = ComputeNormalizedDeviceCoordinates(positionVS, projMatrix);

        position = (int2)(positionNDC * _ScreenSize.xy);
        xy2      = dot(relPosVS.xy, relPosVS.xy);
    #else
        // floor((pixelCoord + 0.5) + vec * pixelsPerMm)
        // position = pixelCoord + floor(0.5 + vec * pixelsPerMm);
        // position = pixelCoord + round(vec * pixelsPerMm);
        // Note that (int) truncates towards 0, while floor() truncates towards -Inf!
        position = pixelCoord + (int2)round((pixelsPerMm * r) * float2(cosPsi, sinPsi));
        xy2      = r * r;
    #endif

    float4 textureSample = LoadSample(position, cacheOffset);
    float3 irradiance    = textureSample.rgb;

    // Compute bilateral weighting.
    float  viewZ  = textureSample.a;
    float  relZ   = viewZ - linearDepth;
    float3 weight = ComputeBilateralWeight(xy2, relZ, mmPerUnit, S, rcpPdf);

    // For the SSS Occlusion, we take in account all the samples as weight
    // This is required if we want darkening on zones where less samples are available due to masking.
    #if USE_SSS_OCCLUSION
    totalWeight     += weight;
    #endif

    // Check the results of the stencil test.
    if (TestLightingForSSS(irradiance))
    {
        int sampleProfileIndex = GetDiffusionProfileIndexAtPosition(position);
        if (SameProfiles(sampleProfileIndex, currentProfileIndex))
        {
            // Note: if the texture sample if off-screen, (z = 0) -> (viewZ = far) -> (weight â‰ˆ 0).
            totalIrradiance += weight * irradiance;
            #if !USE_SSS_OCCLUSION
            totalWeight     += weight;
            #endif
        }
        else
        {
            totalIrradiance += weight * irradiance * borderAttenuationColor;
        }
    }
    else
    {
        // The irradiance is 0. This could happen for 2 reasons.
        // Most likely, the surface fragment does not have an SSS material.
        // Alternatively, our sample comes from a region without any geometry.
        // Our blur is energy-preserving, so 'centerWeight' should be set to 0.
        // We do not terminate the loop since we want to gather the contribution
        // of the remaining samples (e.g. in case of hair covering skin).
    }

#if SSS_DEBUG_PATTERN
    debugInfo.testCoordHit = debugInfo.testCoordHit || all(debugInfo.testCoord == position);
#endif
}

void StoreResult(uint2 pixelCoord, float3 irradiance)
{
#ifdef USE_INTERMEDIATE_BUFFER
    _CameraFilteringTexture[COORD_TEXTURE2D_X(pixelCoord)] = float4(irradiance, 1);
#else
    _CameraColorTexture[COORD_TEXTURE2D_X(pixelCoord)]    += float4(irradiance, 0);
#endif
}

struct CacheInfo
{
    uint2 cacheCoord;
    int2 cacheOffset;
};

struct CenterInfo
{
    float3 irradiance;
    float depth;
};

struct SssResult
{
    float3 irradiance;
};

void EvaluateSss(uint2 pixelCoord, CacheInfo cacheInfo, CenterInfo centerInfo, inout DebugInfo debugInfo, out SssResult result)
{
    // Initialize the result
    result = (SssResult)0;

    PositionInputs posInput = GetPositionInput(pixelCoord, _ScreenSize.zw);

    // The result of the stencil test allows us to statically determine the material type (SSS).
    SSSData sssData;
    DECODE_FROM_SSSBUFFER(posInput.positionSS, sssData);

    int    profileIndex = sssData.diffusionProfileIndex;
    float  distScale = sssData.subsurfaceMask;
    float3 S = _ShapeParamsAndMaxScatterDists[profileIndex].rgb;
    float  d = _ShapeParamsAndMaxScatterDists[profileIndex].a;
    float3 borderAttenuationColor = _BorderAttenuationColor[profileIndex].rgb;
    float  metersPerUnit = _WorldScalesAndFilterRadiiAndThicknessRemaps[profileIndex].x;
    float  filterRadius = _WorldScalesAndFilterRadiiAndThicknessRemaps[profileIndex].y; // In millimeters

    // Reconstruct the view-space position corresponding to the central sample.
    float2 centerPosNDC = posInput.positionNDC;
    float2 cornerPosNDC = centerPosNDC + 0.5 * _ScreenSize.zw;
    float3 centerPosVS = ComputeViewSpacePosition(centerPosNDC, centerInfo.depth, UNITY_MATRIX_I_P);
    float3 cornerPosVS = ComputeViewSpacePosition(cornerPosNDC, centerInfo.depth, UNITY_MATRIX_I_P);

    // Rescaling the filter is equivalent to inversely scaling the world.
    float mmPerUnit = MILLIMETERS_PER_METER * (metersPerUnit * rcp(distScale));
    float unitsPerMm = rcp(mmPerUnit);

    // Compute the view-space dimensions of the pixel as a quad projected onto geometry.
    // Assuming square pixels, both X and Y are have the same dimensions.
    float unitsPerPixel = max(0.0001f, 2 * abs(cornerPosVS.x - centerPosVS.x));
    float pixelsPerMm = rcp(unitsPerPixel) * unitsPerMm;

    // Area of a disk.
    float filterArea = PI * Sq(filterRadius * pixelsPerMm);
    uint  sampleCount = (uint)(filterArea * rcp(SSS_PIXELS_PER_SAMPLE));
    uint  sampleBudget = (uint)_SssSampleBudget;

    uint   texturingMode = GetSubsurfaceScatteringTexturingMode(profileIndex);
    float3 albedo = ApplySubsurfaceScatteringTexturingMode(texturingMode, sssData.diffuseColor);

    if (distScale == 0 || sampleCount < 1)
    {
#if SSS_DEBUG_LOD
        float3 green = float3(0, 1, 0);
        result.irradiance = green;
#else
        result.irradiance = albedo * centerInfo.irradiance;
#endif
        return;
    }

#if SSS_DEBUG_LOD
    float3 red = float3(1, 0, 0);
    float3 blue = float3(0, 0, 1);
    result.irradiance = lerp(blue, red, saturate(sampleCount * rcp(sampleBudget)));
    return;
#endif

    float4x4 viewMatrix, projMatrix;
    GetLeftHandedViewSpaceMatrices(viewMatrix, projMatrix);

    // TODO: Since we have moved to forward SSS, we don't support anymore a bsdfData.normalWS.
    // Once we include normal+roughness rendering during the prepass, we will have a buffer to bind here and we will be able to reuse this part of the algorithm on demand.
#if SSS_USE_TANGENT_PLANE
#error ThisWillNotCompile_SeeComment
// Compute the tangent frame in view space.
    float3 normalVS = mul((float3x3)viewMatrix, bsdfData.normalWS);
    float3 tangentX = GetLocalFrame(normalVS)[0] * unitsPerMm;
    float3 tangentY = GetLocalFrame(normalVS)[1] * unitsPerMm;
#else
    float3 normalVS = float3(0, 0, 0);
    float3 tangentX = float3(0, 0, 0);
    float3 tangentY = float3(0, 0, 0);
#endif

#if SSS_DEBUG_NORMAL_VS
    // We expect the normal to be front-facing.
    float3 viewDirVS = normalize(centerPosVS);
    if (dot(normalVS, viewDirVS) >= 0)
    {
        result.irradiance = float3(1, 1, 1);
        return;
    }
#endif

#if SSS_RANDOM_ROTATION
    // Note that GenerateHashedRandomFloat() only uses the 23 low bits, hence the 2^24 factor.
    float phase = TWO_PI * GenerateHashedRandomFloat(uint3(pixelCoord, (uint)(centerInfo.depth * 16777216)));
#else
    float phase = 0;
#endif

    uint n = min(sampleCount, sampleBudget);

    // Accumulate filtered irradiance and bilateral weights (for renormalization).
    float3 centerWeight = 0; // Defer (* albedo)
    float3 totalIrradiance = 0;
    float3 totalWeight = 0;

    float linearDepth = LinearEyeDepth(centerInfo.depth, _ZBufferParams);
    for (uint i = 0; i < n; i++)
    {
        // Integrate over the image or tangent plane in the view space.
        EvaluateSample(i, n, pixelCoord, cacheInfo.cacheOffset,
            S, d, centerPosVS, mmPerUnit, pixelsPerMm,
            phase, tangentX, tangentY, projMatrix, linearDepth, profileIndex,
            borderAttenuationColor, debugInfo, totalIrradiance, totalWeight);
    }

    // Total weight is 0 for color channels without scattering.
    totalWeight = max(totalWeight, FLT_MIN);

#if SSS_DEBUG_PATTERN
    debugInfo.withinRadius = length(float2(pixelCoord) - float2(debugInfo.testCoord)) * rcp(pixelsPerMm) < filterRadius;
#endif
    result.irradiance = albedo * (totalIrradiance / totalWeight);
}

[numthreads(GROUP_SIZE_2D, 1, 1)]
void SubsurfaceScattering(uint3 groupId          : SV_GroupID,
                          uint  groupThreadId    : SV_GroupThreadID,
                          uint3 dispatchThreadId : SV_DispatchThreadID)
{
    groupThreadId &= GROUP_SIZE_2D - 1; // Help the compiler
    UNITY_XR_ASSIGN_VIEW_INDEX(dispatchThreadId.z);

    // Note: any factor of 64 is a suitable wave size for our algorithm.
    uint waveIndex = WaveReadLaneFirst(groupThreadId / 64);
    uint laneIndex = groupThreadId % 64;
    uint quadIndex = laneIndex / 4;

    // Arrange threads in the Morton order to optimally match the memory layout of GCN tiles.
    uint2 groupCoord  = DecodeMorton2D(groupThreadId);
    uint2 groupOffset = groupId.xy * GROUP_SIZE_1D;
    uint2 pixelCoord  = groupOffset + groupCoord;

    CacheInfo cacheInfo;
    cacheInfo.cacheOffset = (int2)groupOffset - TEXTURE_CACHE_BORDER;
    cacheInfo.cacheCoord = groupCoord + TEXTURE_CACHE_BORDER;

    if (groupThreadId == 0)
    {
        uint stencilRef = STENCILUSAGE_SUBSURFACE_SCATTERING;

        // Check whether the thread group needs to perform any work.
        uint s00Address = Get1DAddressFromPixelCoord(2 * groupId.xy + uint2(0, 0), _CoarseStencilBufferSize.xy, groupId.z);
        uint s10Address = Get1DAddressFromPixelCoord(2 * groupId.xy + uint2(1, 0), _CoarseStencilBufferSize.xy, groupId.z);
        uint s01Address = Get1DAddressFromPixelCoord(2 * groupId.xy + uint2(0, 1), _CoarseStencilBufferSize.xy, groupId.z);
        uint s11Address = Get1DAddressFromPixelCoord(2 * groupId.xy + uint2(1, 1), _CoarseStencilBufferSize.xy, groupId.z);

        uint s00 = _CoarseStencilBuffer[s00Address];
        uint s10 = _CoarseStencilBuffer[s10Address];
        uint s01 = _CoarseStencilBuffer[s01Address];
        uint s11 = _CoarseStencilBuffer[s11Address];

        uint HTileValue = s00 | s10 | s01 | s11;
        // Perform the stencil test (reject at the tile rate).
        processGroup = ((HTileValue & stencilRef) != 0);
    }

    // Wait for the LDS.
    GroupMemoryBarrierWithGroupSync();

    if (!processGroup) { return; }

    CenterInfo centerInfo;
    centerInfo.irradiance  = LOAD_TEXTURE2D_X(_IrradianceSource, pixelCoord).rgb;
    centerInfo.depth       = 0;
    bool   passedStencilTest = TestLightingForSSS(centerInfo.irradiance);

    // Save some bandwidth by only loading depth values for SSS pixels.
#if !USE_SSS_OCCLUSION // When Occlusion is enabled we need all the samples in the cache to be valid.
    if (passedStencilTest)
#endif
    {
        centerInfo.depth = LOAD_TEXTURE2D_X(_DepthTexture, pixelCoord).r;
    }

#if SSS_USE_LDS_CACHE
    // Populate the central region of the LDS cache.
    StoreSampleToCacheMemory(float4(centerInfo.irradiance, centerInfo.depth), cacheInfo.cacheCoord);

    uint numBorderQuadsPerWave = TEXTURE_CACHE_SIZE_1D / 2 - 1;
    uint halfCacheWidthInQuads = TEXTURE_CACHE_SIZE_1D / 4;

    if (quadIndex < numBorderQuadsPerWave)
    {
        // Fetch another texel into the LDS.
        uint2 startQuad = halfCacheWidthInQuads * DeinterleaveQuad(waveIndex);

        uint2 quadCoord;

        // The traversal order is such that the quad's X coordinate is monotonically increasing.
        // The corner is always the near the block of the corresponding wavefront.
        // Note: the compiler can heavily optimize the code below, as the switch is scalar,
        // and there are very few unique values due to the symmetry.
        switch (waveIndex)
        {
            case 0:  // Bottom left
                quadCoord.x = max(0, (int)(quadIndex - (halfCacheWidthInQuads - 1)));
                quadCoord.y = max(0, (int)((halfCacheWidthInQuads - 1) - quadIndex));
                break;
            case 1:  // Bottom right
                quadCoord.x = min(quadIndex, halfCacheWidthInQuads - 1);
                quadCoord.y = max(0, (int)(quadIndex - (halfCacheWidthInQuads - 1)));
                break;
            case 2:  // Top left
                quadCoord.x = max(0, (int)(quadIndex - (halfCacheWidthInQuads - 1)));
                quadCoord.y = min(quadIndex, halfCacheWidthInQuads - 1);
                break;
            default: // Top right
                quadCoord.x = min(quadIndex, halfCacheWidthInQuads - 1);
                quadCoord.y = min(halfCacheWidthInQuads - 1, 2 * (halfCacheWidthInQuads - 1) - quadIndex);
                break;
        }

        uint2  cacheCoord2 = 2 * (startQuad + quadCoord) + DeinterleaveQuad(laneIndex);
        int2   pixelCoord2 = (int2)(groupOffset + cacheCoord2) - TEXTURE_CACHE_BORDER;
        float3 irradiance2 = LOAD_TEXTURE2D_X(_IrradianceSource, pixelCoord2).rgb;
        float  depth2      = 0;

        // Save some bandwidth by only loading depth values for SSS pixels.
        if (TestLightingForSSS(irradiance2))
        {
            depth2 = LOAD_TEXTURE2D_X(_DepthTexture, pixelCoord2).r;
        }

        // Populate the border region of the LDS cache.
        StoreSampleToCacheMemory(float4(irradiance2, depth2), cacheCoord2);
    }

    // Wait for the LDS.
    GroupMemoryBarrierWithGroupSync();
#endif

    if (!passedStencilTest) { return; }

    DebugInfo debugInfo;
    debugInfo = (DebugInfo)0;

    SssResult result;
    EvaluateSss(pixelCoord, cacheInfo, centerInfo, debugInfo, result);

    float3 irradiance = result.irradiance;

#if SSS_DEBUG_PATTERN
    // Find the center of the screen
    uint2 screenCenter = uint2(_ScreenSize.xy / 2);

    // Evaluate SSS again at the pixel in the middle of the screen
    // using the current pixel coordinate as the test pixel
    debugInfo.testCoord = pixelCoord;
    debugInfo.testCoordHit = false;
    debugInfo.withinRadius = false;

    // refresh with data from the sceen center
    centerInfo.irradiance = LOAD_TEXTURE2D_X(_IrradianceSource, screenCenter).rgb;
    centerInfo.depth = LOAD_TEXTURE2D_X(_DepthTexture, screenCenter).r;

    EvaluateSss(screenCenter, cacheInfo, centerInfo, debugInfo, result);

    // Draw a lighter circle indicating profile radius
    irradiance += debugInfo.withinRadius ? float3(0.1, 0.1, 0.1) : float3(0, 0, 0);

    // Draw dashed threaed group border
    irradiance = any(pixelCoord % GROUP_SIZE_1D == 0) && all(pixelCoord % 2 == 0) ? float3(0, 0, 0) : irradiance;

    // Draw sample positions as white pixels
    irradiance = debugInfo.testCoordHit ? float3(1, 1, 1) : irradiance;
#endif

    StoreResult(pixelCoord, irradiance);
}

#ifdef PACK_DIFFUSION_PROFILE
[numthreads(8, 8, 1)]
void PackDiffusionProfile(uint3 groupId          : SV_GroupID,
                          uint  groupThreadId    : SV_GroupThreadID,
                          uint3 dispatchThreadId : SV_DispatchThreadID)
{

    UNITY_XR_ASSIGN_VIEW_INDEX(dispatchThreadId.z);
    uint2 pixelCoord0 = uint2(dispatchThreadId.x * 2, dispatchThreadId.y);
    uint2 pixelCoord1 = pixelCoord0 + uint2(1, 0);
    uint packedProfiles = 0;

    // The result of the stencil test allows us to statically determine the material type (SSS).
    SSSData sssData;
    DECODE_FROM_SSSBUFFER(pixelCoord0, sssData);
    packedProfiles |= sssData.diffusionProfileIndex & 0xF;

    DECODE_FROM_SSSBUFFER(pixelCoord1, sssData);
    packedProfiles |= (sssData.diffusionProfileIndex & 0xF) << 4;

    _DiffusionProfileIndexTexture[COORD_TEXTURE2D_X(dispatchThreadId.xy)] = packedProfiles;
}
#endif
