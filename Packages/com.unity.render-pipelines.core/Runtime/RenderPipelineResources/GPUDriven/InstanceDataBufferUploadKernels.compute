#pragma kernel MainUploadScatterInstances

//#pragma enable_d3d11_debug_symbols

int _InputComponentsCount;
int _InputInstancesCount;
ByteAddressBuffer _InputInstanceData;
ByteAddressBuffer _InputInstanceIndices;
ByteAddressBuffer _InputComponentAddresses;
ByteAddressBuffer _OutputComponentByteCounts;
ByteAddressBuffer _OutputComponentIndices;
ByteAddressBuffer _OutputComponentInstanceIndexRanges;
ByteAddressBuffer _OutputComponentIsPerInstance;
ByteAddressBuffer _OutputComponentAddresses;
RWByteAddressBuffer _OutputBuffer;

[numthreads(64, 1, 1)]
void MainUploadScatterInstances(uint3 dispatchThreadID : SV_DispatchThreadID)
{
    uint inputInstanceIndex = dispatchThreadID.x;
    
    if (inputInstanceIndex >= (uint) _InputInstancesCount)
        return;

    uint outputInstanceIndex = _InputInstanceIndices.Load(inputInstanceIndex << 2);

    [loop]
    for (int inputComponent = 0; inputComponent < _InputComponentsCount; ++inputComponent)
    {
        uint outputComponent = _OutputComponentIndices.Load(inputComponent << 2);
        uint instanceIndexRangeBegin = _OutputComponentInstanceIndexRanges.Load(outputComponent * 8);
        uint instanceIndexRangeEnd = _OutputComponentInstanceIndexRanges.Load(outputComponent * 8 + 4);

        // Not every instance has all the components.
        if (outputInstanceIndex < instanceIndexRangeBegin || outputInstanceIndex >= instanceIndexRangeEnd)
            continue;
        
        uint inputComponentAddress = _InputComponentAddresses.Load(inputComponent << 2);
        uint componentByteSize = _OutputComponentByteCounts.Load(outputComponent << 2);
        uint outputComponentAddress = _OutputComponentAddresses.Load(outputComponent << 2);

        // Some components are shared between all instances.
        uint isPerInstanceComponent = _OutputComponentIsPerInstance.Load(outputComponent << 2);

        uint inputAddress = inputComponentAddress + inputInstanceIndex * componentByteSize * isPerInstanceComponent;
        uint outputAddress = outputComponentAddress + outputInstanceIndex * componentByteSize * isPerInstanceComponent;
        
        for (uint uintIndex = 0; uintIndex < componentByteSize / 4u; ++uintIndex)
            _OutputBuffer.Store(outputAddress + uintIndex * 4, _InputInstanceData.Load(inputAddress + uintIndex * 4));
    }
}
